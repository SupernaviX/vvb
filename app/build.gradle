apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

static def generatedString(value) {
    if (value == null) {
        return "null"
    }
    return "\"${value}\""
}

def tryReadConfig(filename) {
    def config = [:]
    def file = new File(project.rootDir, filename)
    if (!file.exists()) {
        return config
    }
    file.readLines().each {
        if (!it.isEmpty() && !it.startsWith('#')) {
            def (key, value) = it.split("=", 2)
            config[key] = value
        }
    }
    return config
}

repositories {
    maven {
        url 'https://leiainc.jfrog.io/leiainc/gradle-release'
    }
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    buildFeatures {
        viewBinding true
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['./../distribution/leia_sdk/lib']
        }
    }
    packagingOptions {
        pickFirst "**/lib/**"
    }

    defaultConfig {
        applicationId "com.simongellis.vvb"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 20
        versionName "1.6.3"

        buildConfigField("String", "ACRA_URI", generatedString("https://vvb-notifications.simongellis.com/report"))

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            buildConfigField("boolean", "ACRA_ENABLED", "false")
            buildConfigField("String", "ACRA_BASIC_AUTH_LOGIN", "null")
            buildConfigField("String", "ACRA_BASIC_AUTH_PASSWORD", "null")
        }
        release {
            buildConfigField("boolean", "ACRA_ENABLED", "true")
            def config = tryReadConfig("release.env")
            buildConfigField("String", "ACRA_BASIC_AUTH_LOGIN", generatedString(config.login))
            buildConfigField("String", "ACRA_BASIC_AUTH_PASSWORD", generatedString(config.password))

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    ndkVersion = "22.1.7171670"
}

apply plugin: 'org.mozilla.rust-android-gradle.rust-android'

// The cardboard SDK stores libraries in target-specific folders,
// so grab the right library for the toolchain we're compiling now
def includeCardboardSdk(spec, toolchain) {
    def folder = toolchain.folder - ~/^android\//
    def cardboardSdkLocation = "$project.rootDir/app/libraries/jni/$folder"
    spec.environment("RUSTFLAGS", "-L $cardboardSdkLocation -l android")

    // Copy the right SDK into the project as well
    copy {
        from cardboardSdkLocation
        into "$buildDir/rustJniLibs/$toolchain.folder"
        include "lib*.so"
    }
}

cargo {
    module = ".."
    libname = "vvb"
    targets = ['arm', 'arm64', 'x86', 'x86_64']
    profile = "release"
    prebuiltToolchains = true
    exec { spec, target -> includeCardboardSdk (spec, target) }
}

// The dependencies for NDK builds live inside the .aar files so they need to
// be extracted before NDK targets can link against.
task extractNdk(type: Copy)  {
    if (file("${project.rootDir}/cardboard-sdk/sdk-release.aar").exists()) {
        copy {
            from zipTree("${project.rootDir}/cardboard-sdk/sdk-release.aar")
            into "libraries/"
            include "jni/**/libcardboard_api.so"
        }
    }
}

task deleteNdk(type: Delete) {
    delete "libraries/jni"
    delete "libraries/cardboard.h"
}

build.dependsOn(extractNdk)
clean.dependsOn(deleteNdk)

tasks.whenTaskAdded { task ->
    if ((task.name == 'javaPreCompileDebug' || task.name == 'javaPreCompileRelease')) {
        task.dependsOn 'cargoBuild'
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(':cardboard-sdk')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.5.0'
    implementation 'androidx.appcompat:appcompat:1.4.0-alpha02'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.preference:preference-ktx:1.1.1"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // relinker
    implementation "com.getkeepsafe.relinker:relinker:1.4.3"

    // cardboard dependencies
    implementation 'com.google.android.gms:play-services-vision:20.1.3'
    implementation 'com.google.protobuf:protobuf-javalite:3.8.0'

    // color picker
    implementation 'com.github.kizitonwose.colorpreference:support:1.1.0'

    // file picker
    implementation 'com.nononsenseapps:filepicker:4.2.1'

    // error instrumentation
    def acraVersion = '5.8.3'
    implementation("ch.acra:acra-http:$acraVersion")
    implementation("ch.acra:acra-toast:$acraVersion")

    implementation 'com.leia:leiasdk:4.5.3'
}