import com.nishtahir.CargoBuildTask

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlin_version"
}

static def generatedString(value) {
    if (value == null) {
        return "null"
    }
    return "\"${value}\""
}

def tryReadConfig(filename) {
    def config = [:]
    def file = new File(project.rootDir, filename)
    if (!file.exists()) {
        return config
    }
    file.readLines().each {
        if (!it.isEmpty() && !it.startsWith('#')) {
            def (key, value) = it.split("=", 2)
            config[key] = value
        }
    }
    return config
}

repositories {
    maven {
        url 'https://leiainc.jfrog.io/leiainc/gradle-release'
    }
}

android {
    compileSdk 35

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
        freeCompilerArgs += [
                "-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
        ]
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['./../distribution/leia_sdk/lib']
        }
    }

    defaultConfig {
        applicationId "com.simongellis.vvb"
        minSdkVersion 21
        targetSdkVersion 35
        versionCode 46
        versionName "1.17.1"

        buildConfigField("String", "ACRA_URI", generatedString("https://vvb-notifications.simongellis.com/report"))

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            buildConfigField("boolean", "ACRA_ENABLED", "false")
            buildConfigField("String", "ACRA_BASIC_AUTH_LOGIN", "null")
            buildConfigField("String", "ACRA_BASIC_AUTH_PASSWORD", "null")
        }
        release {
            buildConfigField("boolean", "ACRA_ENABLED", "true")
            def config = tryReadConfig("release.env")
            buildConfigField("String", "ACRA_BASIC_AUTH_LOGIN", generatedString(config.login))
            buildConfigField("String", "ACRA_BASIC_AUTH_PASSWORD", generatedString(config.password))

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions = ['version']

    productFlavors {
        playStore {
            dimension 'version'
        }
        leia {
            dimension 'version'
            applicationIdSuffix '.leia'
            minSdkVersion 30
        }
    }

    packagingOptions {
        jniLibs {
            // https://issuetracker.google.com/issues/147096055#comment3
            useLegacyPackaging = true
            pickFirsts += ['**/lib/**']
        }
        resources {
            pickFirsts += ['**/lib/**', 'app']
        }
    }

    ndkVersion = "28.0.13004108"
    namespace 'com.simongellis.vvb'
    androidResources {
        noCompress = ['vb']
    }
}

apply plugin: 'org.mozilla.rust-android-gradle.rust-android'

// The cardboard SDK stores libraries in target-specific folders,
// so grab the right library for the toolchain we're compiling now.
// Return additional rustflags to link against it
def includeCardboardSdk(toolchain) {
    def folder = toolchain.folder - ~/^android\//
    def cardboardSdkLocation = "$project.rootDir/app/libraries/jni/$folder"

    // Copy the right SDK into the project as well
    copy {
        from cardboardSdkLocation
        into "${project.layout.buildDirectory.asFile.get()}/rustJniLibs/$toolchain.folder"
        include "lib*.so"
    }

    return "-L $cardboardSdkLocation -l android"
}

// Newer NDK versions don't expose their runtimes in ways convenient to rust,
// so we need to copy libraries from its toolchains int othe right place.
// Return additional rustflags to link against this
def linkAgainstToolchain(toolchain) {
    var toolchainDir = new File(android.ndkDirectory, "toolchains/llvm/prebuilt").listFiles().first()

    // We need libc++_shared.so at runtime
    copy {
        from "${toolchainDir.path}/sysroot/usr/lib/${toolchain.compilerTriple}"
        into "${project.layout.buildDirectory.asFile.get()}/rustJniLibs/$toolchain.folder"
        include("libc++_shared.so")
    }

    // We need clang's runtime lib at compile time.
    var clangLibsDir = new File(toolchainDir, "lib/clang").listFiles().last()
    var ndkLibsDir = "${clangLibsDir}/lib/linux"
    var rtLib
    switch (toolchain.platform) {
        case "arm":
            rtLib = "clang_rt.builtins-arm-android"
            break
        case "arm64":
            rtLib = "clang_rt.builtins-aarch64-android"
            break
        case "x86":
            rtLib = "clang_rt.builtins-i686-android"
            break
        case "x86_64":
            rtLib = "clang_rt.builtins-x86_64-android"
            break
        default:
            throw new Exception("Unrecognized platform ${toolchain.platform}")
    }
    return "-L ${ndkLibsDir} -l ${rtLib} -l c++_shared"
}

cargo {
    module = ".."
    libname = "vvb"
    targets = ['arm', 'arm64', 'x86', 'x86_64']
    profile = "release"
    prebuiltToolchains = true
    rustupChannel = 'nightly'
    exec { spec, target ->
        var rustflags = [includeCardboardSdk(target), linkAgainstToolchain(target)].join(" ")
        spec.environment("RUSTFLAGS", rustflags)
    }
}

// The dependencies for NDK builds live inside the .aar files so they need to
// be extracted before NDK targets can link against.
tasks.register('extractNdk', Copy) {
    from zipTree("${project.rootDir}/cardboard-sdk/sdk-release.aar")
    into "libraries/"
    include "jni/**/lib*.so"
}

tasks.register('deleteNdk', Delete) {
    delete "libraries"
}

build.dependsOn(extractNdk)
clean.dependsOn(deleteNdk)

tasks.configureEach { task ->
    if (task.name.endsWith('JniLibFolders')) {
        task.dependsOn 'cargoBuild'
    }
    if (task.name.startsWith('cargoBuild')) {
        task.dependsOn 'extractNdk'
    }
}

project.afterEvaluate {
    // collect all the target dirs we compile to
    def jniTargetDirs = []
    tasks.withType(CargoBuildTask).forEach {
        jniTargetDirs += new File("${project.layout.buildDirectory.asFile.get()}/rustJniLibs", it.toolchain.folder)
    }

    // make sure we rerun the "merge jni lib folders" tasks any time they change
    tasks.matching { it.name.matches(/merge.*JniLibFolders/) }.forEach {
        jniTargetDirs.forEach { dir -> it.inputs.dir(dir) }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(':cardboard-sdk')
    leiaImplementation project(":leia-cnsdk")
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.3.0'
    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation "androidx.preference:preference-ktx:1.2.1"
    implementation "androidx.recyclerview:recyclerview:1.4.0"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'

    // lifecycle things
    def lifecycleVersion = '2.8.7'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"

    // relinker
    implementation "com.getkeepsafe.relinker:relinker:1.4.5"

    // serializer
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.2.2"

    // cardboard dependencies
    implementation 'com.google.android.gms:play-services-vision:20.1.3'
    implementation 'com.google.protobuf:protobuf-javalite:3.19.4'

    // color picker
    implementation 'com.github.kizitonwose.colorpreference:core:1.1.0'
    implementation 'com.github.kizitonwose.colorpreference:support:1.1.0'

    // file picker
    implementation 'com.nononsenseapps:filepicker:4.2.1'

    // flow-emitting preferences client
    implementation 'com.fredporciuncula:flow-preferences:1.5.0'

    // error instrumentation
    def acraVersion = '5.9.7'
    implementation("ch.acra:acra-http:$acraVersion")
    implementation("ch.acra:acra-toast:$acraVersion")

    implementation 'com.leia:leiasdk:4.5.3'

    // custom color picker
    implementation 'com.github.yukuku:ambilwarna:2.0.1'
}