apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.1"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    defaultConfig {
        applicationId "com.simongellis.vvb"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

apply plugin: 'org.mozilla.rust-android-gradle.rust-android'

// The cardboard SDK stores libraries in target-specific folders,
// so grab the right library for the toolchain we're compiling now
def includeCardboardSdk(spec, toolchain) {
    def folder = toolchain.folder - ~/^android\//
    def cardboardSdkLocation = "${project.rootDir}/app/libraries/jni/${folder}"
    spec.environment("RUSTFLAGS", "-L ${cardboardSdkLocation}")

    // Copy the right SDK into the project as well
    copy { copySpec ->
        copySpec.from(cardboardSdkLocation)
        copySpec.into("${buildDir}/rustJniLibs/${toolchain.folder}")
        copySpec.include("lib*.so")
    }
}

cargo {
    module = ".."
    libname = "vvb"
    targets = ['arm', 'arm64', 'x86', 'x86_64']
    profile = "release"
    prebuiltToolchains = true
    exec { spec, target -> includeCardboardSdk (spec, target) }
}

// The dependencies for NDK builds live inside the .aar files so they need to
// be extracted before NDK targets can link against.
task extractNdk(type: Copy)  {
    if (file("${project.rootDir}/cardboard-sdk/sdk-release.aar").exists()) {
        copy {
            from zipTree("${project.rootDir}/cardboard-sdk/sdk-release.aar")
            into "libraries/"
            include "jni/**/libcardboard_api.so"
            include "jni/**/libc++_shared.so"
        }
        copy {
            from "${project.rootDir}/cardboard-sdk/include/cardboard.h"
            into "libraries/"
        }
    }
}

task deleteNdk(type: Delete) {
    delete "libraries/jni"
    delete "libraries/cardboard.h"
}

build.dependsOn(extractNdk)
clean.dependsOn(deleteNdk)

tasks.whenTaskAdded { task ->
    if ((task.name == 'javaPreCompileDebug' || task.name == 'javaPreCompileRelease')) {
        task.dependsOn 'cargoBuild'
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(':cardboard-sdk')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.preference:preference:1.1.0"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // cardboard dependencies
    // Android Mobile Vision
    implementation 'com.google.android.gms:play-services-vision:15.0.2'
    implementation 'com.google.protobuf:protobuf-lite:3.0.0'
}